// Pulse Generator
// ---
// based on Miller Puckette Pulse Train Generator
// from "The Theory and Technique of Electronic Music"
//
// This script uses the Clip Chugin
// https://github.com/mariobuoninfante/ChucK_chugins
//
// Copyright 2020 - Mario Buoninfante
// mario.buoninfante@gmail.com


private class DCRemover extends Chubgraph
{
    inlet => Gain subtract => outlet;
    inlet => OnePole onepole  => subtract; 

    0.9995 => float p;
    subtract.op(2);
    onepole.pole(p);
    // for p > 0:
    //     onepole.pole(p)
    // is equivalent to:
    //     onepole.b0(1-p); onepole.a1(-p);
}

public class PulseGen extends Chubgraph
{
    // DSP
    Phasor phasor => Gain pGain => Gain pOffset => Clip clip => SinOsc osc => DCRemover highpass => outlet;
    Step pStep => pOffset;
    Line line => pGain;

    pGain.op(3);
    pStep.next(-0.25);
    osc.sync(1);
    clip.range(-0.25, 0.75);
    line.time(0.01); // interpolation 't' in seconds
    line.target(1);
    

    // MEMBER VARIABLES

    0 => float frequency;
    0 => float mod_index;    
   
    phasor.freq(frequency);


    // MEMBER FUNCTIONS

    function void freq(float f)
    {
	f => frequency;
	phasor.freq(frequency);
    }

    function void index(float i)
    {
	i => mod_index;
	if (mod_index < 0)
	{
	    line.target(1 - mod_index);
	}
	else
	{
	    line.target(1 + mod_index);
	}    
    }
}